set(SIPCMD {{ sip_build_script }})

macro(sanitize_list var_list)
    if(${var_list})
        list(REMOVE_DUPLICATES ${var_list})
        list(REMOVE_ITEM ${var_list} "")
        list(TRANSFORM ${var_list} PREPEND "\"")
        list(TRANSFORM ${var_list} APPEND "\"")
        list(JOIN ${var_list} ", " ${var_list})
        set(${var_list} "${${var_list}}, ")
    else()
        set(${var_list} "")
    endif()
endmacro()

function(add_sip_module MODULE_TARGET MODULE_SIP SIP_FILES HDR_FILES SRC_FILES)
    message(STATUS "SIP: Generating files")
    string(TOUPPER ${CMAKE_BUILD_TYPE} _upper_case_build_type)

    cmake_path(GET MODULE_SIP STEM sip_module_name)
    cmake_path(GET MODULE_SIP FILENAME sip_module_filename)
    cmake_path(GET MODULE_SIP PARENT_PATH sip_module_path)
    cmake_path(SET _module_sip_path NORMALIZE "${sip_module_path}/")

    set(sip_extra_include_dirs)
    foreach (_sip_include ${SIP_FILES})
        cmake_path(GET _sip_include PARENT_PATH _sip_include_path)
        cmake_path(SET _sip_include_path NORMALIZE "${_sip_include_path}/")
        list(APPEND sip_extra_include_dirs ${_sip_include_path})
    endforeach ()

    message(STATUS "SIP: Collecting locations, compiler flags for the sip module")
    # Get the module specified include locations
    get_target_property(sip_include_dirs ${MODULE_TARGET} INTERFACE_INCLUDE_DIRECTORIES)

    # Get the module compiler options
    set(_compile_options)
    get_target_property(_own_compile_options ${MODULE_TARGET} INTERFACE_COMPILE_OPTIONS)
    if(_own_compile_options)
        list(APPEND _compile_options "${_own_compile_options}")
    endif()

    set(_compile_features)
    get_target_property(_own_compile_features ${MODULE_TARGET} INTERFACE_COMPILE_FEATURES)
    if(_own_compile_features)
        foreach(feature ${_own_compile_features})
            if(feature STREQUAL "cxx_std_17")
                list(APPEND _compile_features "-std=c++17")
            elseif(feature STREQUAL "cxx_std_20")
                list(APPEND _compile_features "-std=c++20")
            elseif(feature STREQUAL "cxx_std_23")
                list(APPEND _compile_features "-std=c++23")
            endif()
        endforeach()
    endif()
    set(sip_compile_args)
    list(APPEND sip_compile_args ${_compile_features} ${_compile_options})

    # Get the module define
    get_target_property(sip_define_macros ${MODULE_TARGET} INTERFACE_COMPILE_DEFINITIONS)

    # Sanitize the lists
    sanitize_list(sip_compile_args)
    sanitize_list(sip_extra_include_dirs)
    sanitize_list(sip_define_macros)
    sanitize_list(sip_include_dirs)

    set(sip_sources "${SRC_FILES}")
    sanitize_list(sip_sources)
    set(sip_header_files "${HDR_FILES}")
    sanitize_list(sip_header_files)

    message(STATUS "SIP: Generating pyproject.toml")
    # FIXME: for generator expressions
    configure_file(${CMAKE_CURRENT_BINARY_DIR}/pyproject.toml.pre ${CMAKE_CURRENT_BINARY_DIR}/pyproject.toml)

    # Touch the generated files (8 in total) to make them dirty and force them to rebuild
    set(_sip_output_files)
    list(LENGTH SIP_FILES _no_outputfiles)
    foreach(_concat_file_nr RANGE 0 ${_no_outputfiles})
        if(${_concat_file_nr} LESS 8)
            list(APPEND _sip_output_files "${CMAKE_CURRENT_BINARY_DIR}/${sip_module_name}/${sip_module_name}/sip${sip_module_name}part${_concat_file_nr}.cpp")
        endif()
    endforeach()

    execute_process(
            COMMAND ${SIPCMD}
            WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/
            )  # This will generate the source-files during the configuration step in CMake

    # Find the generated source files
    file(GLOB sip_c "${CMAKE_CURRENT_BINARY_DIR}/${sip_module_name}/${sip_module_name}/*.c")
    file(GLOB sip_cpp "${CMAKE_CURRENT_BINARY_DIR}/${sip_module_name}/${sip_module_name}/*.cpp")
    file(GLOB sip_hdr "${CMAKE_CURRENT_BINARY_DIR}/${sip_module_name}/${sip_module_name}/*.h")

    # create the target library and link all the files (generated and user specified
    add_library("sip_${MODULE_TARGET}" SHARED ${sip_c} ${sip_cpp} ${sip_hdr} ${SRC_FILES})

    # Make sure that the library name of the target is the same as the MODULE_TARGET with the appropriate extension
    target_link_libraries("sip_${MODULE_TARGET}" PRIVATE "${MODULE_TARGET}")
    set_target_properties("sip_${MODULE_TARGET}" PROPERTIES PREFIX "")
    set_target_properties("sip_${MODULE_TARGET}" PROPERTIES OUTPUT_NAME "${MODULE_TARGET}")

    # Add the custom command to (re-)generate the files and mark them as dirty. This allows the user to actually work
    # on the sip definition files without having to reconfigure the complete project.
    # TODO: check if this will also work when the _sip_output_files in-/decrease or that such a scenario requires a
    # full regeneration.
    add_custom_command(
            TARGET "sip_${MODULE_TARGET}"
            COMMAND ${SIPCMD}
            COMMAND ${CMAKE_COMMAND} -E touch ${_sip_output_files}
            WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/
            MAIN_DEPENDENCY ${MODULE_SIP}
            DEPENDS ${SIP_FILES}
            VERBATIM
    )

    # TODO: Installer, for the Python module don't forget about the pyi file for code completion
endfunction()
